plugins {
    id "com.google.protobuf" version "0.8.8"
}
apply plugin: 'java'
apply plugin: 'com.google.protobuf'

group = 'org.gangel.orders.grpc'
description = 'orders-grpc-common'

patchModules.config = [
    "grpc.core=grpc-context-" + ioGrpcVersion +".jar",
    "javax.annotation.api=jsr305-3.0.2.jar"
]

compileJava {
    options.compilerArgs += ['--add-exports', 'javax.annotation.api/javax.annotation.concurrent=grpc.stub']
}

dependencies {
    annotationProcessor "org.projectlombok:lombok:$lombokVersion"
    implementation group: 'org.projectlombok', name: 'lombok', version:lombokVersion
    implementation group: 'com.google.protobuf', name: 'protobuf-java', version:'3.6.1'
    implementation group: 'io.grpc', name: 'grpc-stub', version: ioGrpcVersion
    implementation group: 'io.grpc', name: 'grpc-protobuf', version: ioGrpcVersion
	compileOnly "javax.annotation:javax.annotation-api:1.2"
}

protobuf {
  plugins {
    grpc {
      artifact = "io.grpc:protoc-gen-grpc-java:$ioGrpcVersion"
    }
  }
  generateProtoTasks {
    all()*.plugins {
      grpc {}
    }
  }
}


